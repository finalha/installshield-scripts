//===========================================================================
//
//  File Name:    Setup.rul
//
//  Description:  Blank setup main script file
//
//  Comments:     Blank setup is an empty setup project. If you want to
//				  create a new project via. step-by step instructions use the
//				  Project Assistant.
//
//===========================================================================
#define VIEWVERSION "7.0.0"
#define VERSION_RM_PACKAGE_DATE "Redis v7.0.0 "
#define FeatureRedis "Redis"
#define DLLNAME SUPPORTDIR^"WriteVersion.dll"   
#define SETUPTYPE "Redis"
#define MSG_INFO_BUILD_SETUPTIME 	"Need to save the timestamp of building data."
#define MSG_INFO_STEP_OUT_WELCOME 	"Step out: Welcome."
#define MSG_ERRO_CHECK_ADMIN	 	"You don't have enough privilege to continue the installation, please contact the administrator. If this is Windows 2008, maybe you need rerun the installation program by menu 'Run as administrator'."
#define MSG_INFO_STEP_OUT_SYSINFO 	"Step out: System information"
#define MSG_INFO_STEP_OUT_LICENSE 	"Step out: License"
#define MSG_INFO_STEP_OUT_CUSTOMERINFO 	"Step out: Customer information."
#define MSG_ERR_PATH_FORMAT	 	"The specified folder:\n'%s'\n is invalid,incomplete or write protected.Please type a full path with drive letter;for example'C:\\XXXX'."
#define DISKSPACENOTENOUGH "There is not enough disk space(at least 1G )."
#define DISKSPACE_M 1024
#define MSG_INFO_STEP_OUT_INPUT_ROOTDIR 	"Step out: Input root dir."
#define MSG_INFO_STEP_OUT_STARTCOPY 		"Step out: Start Copy."
#define REDIS_VERSION "3.0.501"
#define REDIS_MSI "Redis-x64-3.0.501.msi"
#define INSTALLSTATUS "Please wait while the InstallShield Wizard installs Redis for NetBrain."
#define MSG_ERR_PORT_INVALID "The port: %s is in use, please enter another!"
#define REGKEY_REDIS   	"Redis" 
#define MSG_INFO_REPEAT_INSTALL "The product of same version can not be installed on the same machine. The installation will abort."
#define MSG_INFO_32BIT_INSTALL "The product is 64bit software, so it can not be installed under the folder of "+PROGRAMFILES+", please choose another one."
#define REGROOT "SOFTWARE\\NetBrain"
#define REGREDIS "SOFTWARE\\NetBrain\\NetBrain Redis"
#define MSG_ERR_DIR_EMPTY			"The specified folder must be empty."
#define MSG_WARN_CHECK_EXTENDEDMEMORY    "The extended memory of your machine is less than 16GB, which may lead to installation failure or off-normal use. Do you want to continue the installation?"
#define MSG_ERR_IP_PORT_SPECCHAR    ",the format of string is wrong or contains some special characters. "
#define MSG_ERR_IP_CONNECTION   ",the IP address cannot be connected. "
#define REG_LOCALDUMPS "SOFTWARE\\Microsoft\\Windows\\Windows Error Reporting\\LocalDumps"
#define DUMP_PATH "C:\\CrashDumps"
//#define STUNNELINSTALLDIR "C:\\Program Files (x86)\\stunnel"
#define STUNNELWITHOUTBINORCONFIGFOLDER "The bin or config folder doesnot exists in the location of stunnel,please choose another one."
#define STUNNELWITHOUTCONF "The stunnel.conf doesnot exists in the config folder of stunnel,please choose another one."
#define STUNNEL_ERR_RENAMECONF "The stunnel.conf cannot be renamed to stunnel.confback."
#define STUNNEL_ERR_MERGE_CERT_KEY "Fail to merge %s and %s to rediscert.pem"
#define STUNNEL_ERR_MODIFYCONF "Fail to modify %s"
#define  MSG_CONFIG_STUNNEL "The installation will take some time to do the configuration of stunnel, please wait."
#define REDIS_MSI_PRODUCTCODE "{C8312FC3-8684-43D4-BB35-9C4673DD260E}"
#define REDIS_MSI_UNINSTALL_PATH "SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Uninstall\\"+REDIS_MSI_PRODUCTCODE
#define REDIS_ALREADY_EXISTS "The service of Redis already exists, the installation will abort"

// Included header files ----------------------------------------------------
prototype HWND kernel32.OpenMutexA(NUMBER, BOOL, BYVAL STRING);
prototype HWND kernel32.CreateMutexA(NUMBER, BOOL, BYVAL STRING);

STRING LOGFILE_PATH;
STRING LOGFILE_NAME;
STRING LOGFILE;
STRING svFeatureRedis;
STRING svCurrentVersion;
NUMBER nvInstallDataDir;
STRING svUserName,szCompany;
STRING svOSVer;
STRING svRedisInstalldir;
STRING svDataSetupTime;
STRING svTempVal;
STRING svDataDirRoot;
NUMBER nResult,nResult1;
HWND hMutex;
STRING svredisip;
//Redis input UI begin
STRING redisPassword, redisPort,stunnelPort,redisConfirmPassword;
STRING redisCertPath, redisPrivateKey, redisTlsVersion;
BOOL redisSsl;
STRING svredisssl;
//Redis input UI end
NUMBER nLocation;
STRING svFileName;
STRING szAdminPrivilege;
STRING stunnelinstalldir;


// Included header files ----------------------------------------------------
#include "ifx.h"
#include "featureevents.rul"

// Note: In order to have your InstallScript function executed as a custom
// action by the Windows Installer, it must be prototyped as an 
// entry-point function.

// The keyword export identifies MyFunction() as an entry-point function.
// The argument it accepts must be a handle to the Installer database.
    
/* export prototype MyFunction(HWND); */

function OnFilterComponents()
string stDateTime;
number nvReturn;
begin 
	// init logfile begin
	LOGFILE_PATH = "c:\\";
	StrSub (LOGFILE_PATH, WINDIR, 0, 3);
	if( REMOVEONLY ) then
		LOGFILE_NAME="nbredisuninstall.log";
	else
		LOGFILE_NAME="nbredisinstall.log";
	endif; 	
	LOGFILE=LOGFILE_PATH^LOGFILE_NAME; 
	DeleteLogFile();
	// init logfile end
	    
    // Filter file groups by current platform.
    FeatureFilterOS(MEDIA, 0, ISOSL_ALL, TRUE);
    FeatureFilterOS(MEDIA, 0, SYSINFO.nISOSL, FALSE);
 
    // Filter file groups by current language.
    FeatureFilterLanguage(MEDIA, ISLANG_ALL, TRUE);
    FeatureFilterLanguage(MEDIA, STANDARD_SELECTED_LANGUAGE, FALSE);
    WriteLogFile("SETUP OnFilterComponents Normal");
    
    //judge redis not exists
	if( !REMOVEONLY ) then
    if ( ServiceExistsService ( "Redis" )) then
    MessageBox(REDIS_ALREADY_EXISTS,WARNING);
    WriteLogFile(REDIS_ALREADY_EXISTS);     
    abort;
    endif;
    endif;
            
    //admin
	if(!SYSINFO.WINNT.bAdmin_Logged_On) then
	szAdminPrivilege = "Privileges : You don't have enough privilege to continue the installation, please contact the administrator. Maybe you need rerun the installation program by menu 'Run as administrator'.";
	MessageBox(szAdminPrivilege,WARNING);
	WriteLogFile(szAdminPrivilege);
	abort;
	endif;
end;




//---------------------------------------------------------------------------
// OnBegin
//
// The OnBegin event is called directly by the framework after the setup
// initializes.
//---------------------------------------------------------------------------
function OnBegin()
begin
	// Check if another installer is running.
	hMutex = OpenMutexA(READ_CONTROL, FALSE, "NetBrain_RedisInstaller");
	
	if (hMutex) then
		CloseHandle(hMutex);
		MessageBox("The installer is already running.", SEVERE);
		abort;
	else
		hMutex = CreateMutexA(NULL, FALSE, "NetBrain_RedisInstaller");
	endif;
     
	// TO DO: you may change default non-UI setting, for example
	//
	// You may also perform your custom initialization steps, check requirements, etc.
	DialogSetInfo(DLG_INFO_ALTIMAGE, SUPPORTDIR ^ "setup_top.bmp", TRUE);
	svFeatureRedis="0";
 	svCurrentVersion="";
 	nvInstallDataDir = 1;
 	svredisip="127.0.0.1";
 	//svamqpport="5672";      
    redisTlsVersion="1.2";
   	svRedisInstalldir=INSTALLDIR^"Redis"; 
   	/*
   	if (MAINTENANCE) then 
   	if(SYSINFO.bIsWow64) then 
    REGDB_OPTIONS = REGDB_OPTIONS | REGDB_OPTION_WOW64_64KEY;
	nvType = REGDB_STRING; 
    nvSize = -1;  
    if (RegDBSetDefaultRoot (HKEY_LOCAL_MACHINE) = 0) then
    RegDBGetKeyValueEx(REGROOT,REGKEY_RM,nvType,svFeatureRM,nvSize);
    RegDBGetKeyValueEx(REGROOT,REGKEY_RABBITMQ,nvType,svFeatureRabbitMQ,nvSize);
    RegDBGetKeyValueEx(REGROOT,REGKEY_REDIS,nvType,svFeatureRedis,nvSize);        
    RegDBGetKeyValueEx(REGROOT,REGKEY_WS,nvType,svFeatureWS,nvSize);
    RegDBGetKeyValueEx(REGROOT,REGKEY_INITDATA,nvType,svinitdata,nvSize);
    RegDBGetKeyValueEx(REGROOT,REGKEY_UPGRADEDATA,nvType,svupgradedata,nvSize);
    endif;
    REGDB_OPTIONS = REGDB_OPTIONS & ~REGDB_OPTION_WOW64_64KEY;
    endif;
    endif;
    */
    redisPort="6379";
    stunnelPort="7000";
    stunnelinstalldir=PROGRAMFILES^"stunnel";
	if (ExistsDir (DUMP_PATH) != EXISTS) then
	if (CreateDir (DUMP_PATH) < 0) then
	WriteLogFile("Unable to create directory: "+DUMP_PATH);
	else
	WriteLogFile("DUMP_PATH:"+DUMP_PATH);
	endif;	
    endif;
end;



//---------------------------------------------------------------------------
// OnFirstUIBefore
//
// The OnFirstUIBefore event is called by the framework when the setup is
// running in first install mode. By default this event displays UI allowing
// the end user to specify installation parameters.
//---------------------------------------------------------------------------
function OnFirstUIBefore()
    NUMBER nResult, nSetupType, nvSize, nUser;
    STRING szTitle, szMsg, szQuestion, svName, svCompany, szFile;
    STRING szLicenseFile;
	BOOL bCustom, bIgnore1, bIgnore2;
	NUMBER  bLicenseAccepted;    	 		  //SdLicense variable
    LIST    listData,listInfo;            	  //SdStartCopy and SdShowInfoList variable
    NUMBER  listID,ServicePackID;             //SdShowInfoList variable
    NUMBER  nvReturn1,nvReturn2,nvReturn3;	  //SdShowInfoList variable
    STRING  svReturn,szInfo1,szInfo2,szInfo3; //SdShowInfoList variable
    STRING  rightID, svSTRING;                //SdShowInfoList variable
    //NUMBER  nUser;							  //SdCustomerInformation variable 
    STRING  svResult1,svResult2,svResult3;    //SdFeatureDialog variable 
    //NUMBER  nvType,nvSize;
    NUMBER  nvType;
    NUMBER  nBack;     
    STRING  svTmp,szFindMe;
    NUMBER  iResult;  
    STRING szCaption,szDir;  
    NUMBER nResult1,nResult2,nResult3,nResult4,nResult5,nResultMongodb;     
    STRING svCheckWorkSpace;  
    NUMBER nCompareFlag; 
    STRING svCurrentVer,svCurrentDir,svResult,szIISMSG;	
    STRING szField1,szField2;
    NUMBER nvVar2,nvVar3;
    STRING szCaption1,szCaption2;    
    			
begin
    
    if( REMOVEONLY ) then
        Disable( DIALOGCACHE );
		szMsg = SdLoadString( IDS_IFX_ERROR_PRODUCT_NOT_INSTALLED_UNINST );
   		SdSubstituteProductInfo( szMsg );
		MessageBox( szMsg, SEVERE );
		abort;
    endif;
    
	nSetupType = TYPICAL;
		
    iResult = GetDiskSpaceEx(SUPPORTDIR, GBYTES);
    if(iResult < 1 ) then  
    	szIISMSG = "There is no enough free space in system Driver to Extract installation file";
    	SdShowMsg ("", FALSE); 
    	MessageBox(szIISMSG,WARNING);
    	abort;
    endif;
    nResult1=1;
    nResult2=1;
    nResult3=1;
    nResult4=1;
    nResult5=1;
    
    if !(svCurrentVersion = "") then 
		FormatVer(svCurrentVersion,svCurrentVer); 
	endif;
		
    DeleteFile(SUPPORTDIR ^"builddatetime.txt");   
	RunApplication(SUPPORTDIR ^"getsystemdatetime.bat","\""+SUPPORTDIR^"builddatetime.txt"+"\"",FALSE,TRUE,TRUE);   	
    WriteLogFile(VERSION_RM_PACKAGE_DATE + ReadString(SUPPORTDIR,"builddatetime.txt",FALSE));	
    
    SdShowMsg ("Checking system information", TRUE);     			   
    SHELL_OBJECT_FOLDER = @PRODUCT_NAME;	
    szDir = INSTALLDIR;
    svUserName    = "Windows User";
    szCompany = "";
              	              
   	WriteLogFile( MSG_INFO_BUILD_SETUPTIME ); 
   	GetSystemInfo( DATE, nvReturn1, svResult1 );  
   	svDataSetupTime = svResult1;
   	svDataSetupTime = svDataSetupTime + " ";
   	GetSystemInfo( TIME, nvReturn1, svResult1 );
   	svDataSetupTime = svDataSetupTime + svResult1;
 
Dlg_Start:
  	SdShowMsg ("", FALSE); 
Dlg_SdWelcome:
    szTitle = "";
    szMsg   = "The InstallShield Wizard will install Redis for NetBrain on your machine. To continue the installation, click Next.";
    nResult = SdWelcome( szTitle, szMsg );
    if (nResult = BACK) goto Dlg_Start;  
    WriteLogFile( MSG_INFO_STEP_OUT_WELCOME );
    
Dlg_SdShowInfoList:
 	szTitle = "System Configuration";
    szMsg   = "The system configuration is auto-detected as follows:"; 
    listInfo = ListCreate (STRINGLIST); 
    //get CPU information
    GetSystemInfo (CPU, nvReturn1, svReturn);
    if( nvReturn1 == IS_UNKNOWN ) then
 		szInfo1 = "CPU: Unknown";
    else
    	Sprintf(szInfo1, "CPU: %d", nvReturn1,svReturn);
    endif;
    GetSystemInfo (EXTENDEDMEMORY, nvReturn2, svReturn);
    Sprintf(szInfo2, "Extended Memory: %d MB", nvReturn2/1024,svReturn);

    if(SYSINFO.WINNT.bAdmin_Logged_On) then
      	rightID = "Privileges : Administrative Privileges ";
    else
        rightID = "Privileges : You don't have enough privilege to continue the installation, please contact the administrator. If this is Windows 2008, maybe you need rerun the installation program by menu 'Run as administrator'.";
    endif;
     
    ServicePackID = SYSINFO.WINNT.nServicePack;
    NumToStr ( svSTRING, ServicePackID );    
    
    if(SYSINFO.WINNT.bWinVista_Server2008=TRUE)then 
    	szInfo3 = "Operating System: Windows 2008";
    	if(ServicePackID >0 ) then
    		szInfo3 =  szInfo3+" Service Pack "+   svSTRING;
    	endif;
    endif;
    if(SYSINFO.WINNT.bWin7_Server2008R2=TRUE) then      
   		szInfo3 = "Operating System: Windows 2008 R2";
    	if(ServicePackID >0 ) then
    		szInfo3 =  szInfo3+" Service Pack "+   svSTRING;
    	endif;
    endif;
    if(SYSINFO.WINNT.bWin8=TRUE)then 
    	szInfo3 = "Operating System: Windows 2012";
    	if(ServicePackID >0 ) then
    		szInfo3 =  szInfo3+" Service Pack "+   svSTRING;
    	endif;
    endif;
        if(SYSINFO.WINNT.bWin81=TRUE)then 
    	szInfo3 = "Operating System: Windows 2012 R2";
    	if(ServicePackID >0 ) then
    		szInfo3 =  szInfo3+" Service Pack "+   svSTRING;
    	endif;
    endif;
         
    ListAddString(listInfo, szInfo1, AFTER);
    ListAddString(listInfo, szInfo2, AFTER);
    ListAddString(listInfo, szInfo3, AFTER);
    ListAddString(listInfo, rightID, AFTER); 
    
    nResult = SdShowInfoList ( szTitle, szMsg, listInfo );
    ListDestroy(listInfo);
    if(nResult = BACK) goto Dlg_SdWelcome; 
   
 
    if(rightID = "Privileges : You don't have enough privilege to continue the installation, please contact the administrator. If this is Windows 2008, maybe you need rerun the installation program by menu 'Run as administrator'.") then
    	WriteLogFile( MSG_ERRO_CHECK_ADMIN );
    	MessageBox(MSG_ERRO_CHECK_ADMIN,WARNING);
        abort;
    endif;    
                                          
    WriteLogFile( MSG_INFO_STEP_OUT_SYSINFO );

/* 
Dlg_EXTENDEDMEMORY:
	//if extended memory less than 16G=16384MB~16000MB
    //edit by liuyang 2016.8.24 fix ENG-11961 begin
    if ((nvReturn2/1024) < 8000) then
    nResult = MessageBox(MSG_WARN_CHECK_EXTENDEDMEMORY, MB_YESNO );
		if (nResult = IDNO ) then
		abort;
		endif;
    endif;
    //edit by liuyang 2016.8.24 fix ENG-11961 end
*/
    
Dlg_SdLicense:    
    szTitle    = "";
    nResult    = SdLicenseEx ( szTitle, "", "", SUPPORTDIR ^ "license.rtf", TRUE ); 
    if (nResult = BACK) goto Dlg_SdShowInfoList;  
    WriteLogFile( MSG_INFO_STEP_OUT_LICENSE );
     
Dlg_SdCustomerInformation:
	szTitle = "";
	szMsg   = "";
	nResult = SdCustomerInformation ( szTitle, svUserName, szCompany,nUser);
	if (nResult = BACK) goto Dlg_SdLicense;      
	WriteLogFile( MSG_INFO_STEP_OUT_CUSTOMERINFO + ":" + svUserName + ":" + szCompany   );          

Dlg_SdFeatureDialog: 	
	szTitle = "Destination Location \n    Click Next to install to the default folder, or click Change to choose another one. \n";
    szMsg   = "Install %P to:";
    nResult = SdAskDestPath2 ( szTitle, szMsg, INSTALLDIR);     
    szDir = INSTALLDIR;
    if (nResult = BACK) then
   		goto Dlg_SdCustomerInformation;  
    endif;
    //must be 64bit
   	if (SYSINFO.bIsWow64) then
		szFindMe=PROGRAMFILES; 

		nResult=StrFindEx ( szDir, szFindMe, 0 );
		if (nResult>=0)	then
		
		//StrReplace(szDir,szFindMe,PROGRAMFILES64,0);
		MessageBox(MSG_INFO_32BIT_INSTALL, WARNING);
        goto Dlg_SdFeatureDialog;	
		endif;
     endif;

   	nResult=(szDir[1] = ":") && (szDir[2] = "\\");
   	if(!nResult)then 
   		Sprintf( svTempVal, MSG_ERR_PATH_FORMAT, szDir );
   		WriteLogFile( svTempVal );
    	MessageBox (svTempVal, WARNING);
    	goto Dlg_SdFeatureDialog;
    endif; 

   	INSTALLDIR = szDir;   
   	svDataDirRoot = INSTALLDIR;  
   	   	
   	svRedisInstalldir=svDataDirRoot^"Redis"; 
   	//szdumppath = svDataDirRoot^"CrashDumps";
  	//dir lenth 
  	if(StrLength(szDir)>100) then
        MessageBox("The path is too long.", SEVERE);
        goto Dlg_SdFeatureDialog;
  	endif;
  	
  	if(CheckPathSpecChar(szDir)==0) then
  		goto Dlg_SdFeatureDialog;
  	endif;
  	
  	iResult = GetDiskSpaceEx(szDir, MBYTES);  
  	if (iResult < DISKSPACE_M) then
        MessageBox(DISKSPACENOTENOUGH, SEVERE);
        goto Dlg_SdFeatureDialog;
    endif;  	
   	WriteLogFile( MSG_INFO_STEP_OUT_INPUT_ROOTDIR + ":" + INSTALLDIR  );  
   	
//new install begin
//if( nvInstallDataDir = 1 ) then 

Dlg_SdRedis:
	//szTitle = "Redis Configuration";
    nResult = SdRedis(); 
    if (nResult = BACK) then
       goto Dlg_SdFeatureDialog;       
    endif;
        
    if (redisSsl == BUTTON_CHECKED) then
		svredisssl = "true";
	elseif (redisSsl == BUTTON_UNCHECKED) then
		svredisssl = "false";
	endif;
    
    //some checks begin  
    //check password input
    nResult = StrFindEx(redisPassword," ",0);
    if (nResult<0) then 
        WriteLogFile("Password donot contain spaces.");
    else     	
    	MessageBox("Password cannot contain spaces.",WARNING);
    	goto Dlg_SdRedis;
    endif;  
    StrTrim(redisPassword);
    if (StrCompare ("", redisPassword)=0) then 
      MessageBox ("Password of Redis can not be empty.", SEVERE);
      goto Dlg_SdRedis;
    endif;
    
    //check confirm password input
    nResult = StrFindEx(redisConfirmPassword," ",0);
    if (nResult<0) then 
        WriteLogFile("Confirm Password donot contain spaces.");
    else     	
    	MessageBox("Confirm Password cannot contain spaces.",WARNING);
    	goto Dlg_SdRedis;
    endif;  
    StrTrim(redisConfirmPassword);
    if (StrCompare ("", redisConfirmPassword)=0) then 
      MessageBox ("Confirm Password of Redis can not be empty.", SEVERE);
      goto Dlg_SdRedis;
    endif;
    
    //check password and confirm password match
    if (StrCompare (redisPassword, redisConfirmPassword)!=0) then 
      MessageBox ("Your password and confirmation password do not match.", SEVERE);
      goto Dlg_SdRedis;
    endif;
     
    StrTrim(redisPort);
    StrTrim(stunnelPort);
	
		if (StrCompare ("", redisPort) = 0) then 
			MessageBox ("The port number of Redis can not be empty.", SEVERE);
			goto Dlg_SdRedis;
		endif;

		StrToNum(nvVar2, redisPort); 

		if( nvVar2 > 65535 || nvVar2 < 100 ) then 
			MessageBox ( "The port number of Redis must be between 100 and 65535.", SEVERE );
			goto Dlg_SdRedis; 
		endif;
	
	if (svredisssl = "true") then 
		if (StrCompare("", stunnelPort) = 0) then 
			MessageBox ("The port number of Stunnel can not be empty.", SEVERE);
			goto Dlg_SdRedis;
		endif;

		StrToNum(nvVar2, stunnelPort); 

		if( nvVar2 > 65535 || nvVar2 < 100 ) then 
			MessageBox ( "The port number of Stunnel must be between 100 and 65535.", SEVERE );
			goto Dlg_SdRedis; 
		endif; 

		StrToNum(nvVar2, stunnelPort); 
        StrToNum(nvVar3, redisPort); 
        
		if( nvVar2 = nvVar3) then 
			MessageBox ( "The port number of Stunnel can not equal to the port number of Redis.", SEVERE );
			goto Dlg_SdRedis; 
		endif; 	
	endif;
	
	//only check port when not ssl,or stunnel will use the port
	if (svredisssl = "false") then
	nResult = RunApplication( SystemFolder ^ "cscript.exe", "\"" + SUPPORTDIR ^ "portcheck.vbs"+"\"" + " " + redisPort +" "+"\""+SUPPORTDIR ^ "netstat.txt"+"\"", FALSE,TRUE,TRUE );
    if (nResult!=0) then 
    	Sprintf ( svResult, MSG_ERR_PORT_INVALID, redisPort); 
    	MessageBox(svResult,WARNING);
    	goto Dlg_SdRedis;
    endif;   
    DeleteFile ( SUPPORTDIR ^ "netstat.txt" );
    endif;
    
    if (svredisssl = "true") then 
    nResult=(redisCertPath[1] = ":") && (redisCertPath[2] = "\\");
   	if(!nResult)then 
   		Sprintf( svTempVal, MSG_ERR_PATH_FORMAT, redisCertPath );
   		WriteLogFile( svTempVal );
    	MessageBox (svTempVal, WARNING);
    	goto Dlg_SdRedis;
    endif;
    if(CheckPathSpecChar(redisCertPath)==0) then
  		goto Dlg_SdRedis;
  	endif;
  	
  	nResult=(redisPrivateKey[1] = ":") && (redisPrivateKey[2] = "\\");
   	if(!nResult)then 
   		Sprintf( svTempVal, MSG_ERR_PATH_FORMAT, redisPrivateKey );
   		WriteLogFile( svTempVal );
    	MessageBox (svTempVal, WARNING);
    	goto Dlg_SdRedis;
    endif;
    if(CheckPathSpecChar(redisPrivateKey)==0) then
  		goto Dlg_SdRedis;
  	endif;
  	
  	endif;
    //some checks end        
	WriteLogFile("Redis ip is:"+svredisip);
	/*
	if (svredisssl = "true") then
	WriteLogFile("Redis port is:"+redisSSLPort); 
	else
	WriteLogFile("Redis port is:"+redisPort);
	endif;
	*/
	WriteLogFile("Redis port is:"+redisPort);
	WriteLogFile("Stunnel port is:"+stunnelPort);
	WriteLogFile("Redis Password is:"+redisPassword);
	WriteLogFile("Redis SSL is:"+svredisssl);
	if (svredisssl = "true") then
		goto Dlg_SdConfigStunnel;
	else
		goto Dlg_SdStartCopy;
	endif;
	
 Dlg_SdConfigStunnel:
    szTitle = "Stunnel Location \n";
    szMsg   = "Select folder where stunnel was installed.";
    nResult = SdAskDestPath2 ( szTitle, szMsg,stunnelinstalldir); 
    //SdShowMsg ("Configing stunnel, please wait...", TRUE); 	  
	//MessageBox(MSG_CONFIG_STUNNEL,INFORMATION);
    szDir = stunnelinstalldir; 
    WriteLogFile("The install dir of stunnel is:"+stunnelinstalldir);
    if (nResult = BACK) then
       goto Dlg_SdRedis;       
    endif;
    //判断stunnel目录下是否存在bin和conf目录,不存在则给提示,留在当前界面 
    if (ExistsDir (szDir^"bin") != EXISTS || ExistsDir (szDir^"config") != EXISTS) then
    MessageBox(STUNNELWITHOUTBINORCONFIGFOLDER,WARNING);
    goto Dlg_SdConfigStunnel;
    endif;
    //判断stunnel/config目录下是否存在stunnel.conf,不存在则给提示,留在当前界面 
    if (Is(FILE_EXISTS, szDir^"config"^"stunnel.conf") != TRUE) then
    MessageBox(STUNNELWITHOUTCONF,WARNING);
    goto Dlg_SdConfigStunnel;    
    endif;
    
        		 
 Dlg_SdStartCopy:
    szTitle = "Ready to install Redis for NetBrain";
    szMsg   = "Review the following information before clicking Install to start copying files.";
    listData = ListCreate( STRINGLIST );  
    ListAddString(listData, "Destination Directory:", AFTER);
    ListAddString(listData, "              " + INSTALLDIR, AFTER);
    //ListAddString (listData, "", AFTER);
    ListAddString (listData, "User Information: ", AFTER);
    ListAddString (listData, "              "+"User Name:" + svUserName, AFTER);
    ListAddString (listData, "              "+"Company Name:" + szCompany, AFTER);
    //add installed features info edit by liuyang 2016.05.19 begin
    ListAddString (listData, "Installed Components: ", AFTER);
    ListAddString (listData, "              "+ FeatureRedis, AFTER);
    //add installed features info edit by liuyang 2016.05.19 end
    ListAddString (listData, "", AFTER);
                                           
    nResult = SdStartCopy( szTitle, szMsg, listData );	
   
    if (nResult = BACK) then
	  	if (svredisssl = "true") then
		goto Dlg_SdConfigStunnel;
	else
		goto Dlg_SdRedis;
	endif;
 	endif;
    //fix Bug ENG-9370 edit by liuyang 2016.06.02 end	
    ListDestroy(listData);
   	WriteLogFile( MSG_INFO_STEP_OUT_STARTCOPY ); 
   	
   	Disable(CANCELBUTTON);
    // Added in IS 2009 - Set appropriate StatusEx static text.
    SetStatusExStaticText(INSTALLSTATUS);
	
	NumToStr(svFeatureRedis,nResult2);
    // setup default status
    Enable(STATUSEX);
    return 0;
end;

//---------------------------------------------------------------------------
// OnMaintUIBefore
//
// The OnMaintUIBefore event is called by the framework when the setup is
// running in maintenance mode. By default this event displays UI that
// allows the end user to add or remove features, repair currently
// installed features or uninstall the application.
//---------------------------------------------------------------------------
function OnMaintUIBefore()
	NUMBER nResult, nType, nvSize, nvType;
    STRING szTitle, szMsg, svResult, szCaption; 
    STRING svResult1,svResult2,svResult3;    //SdFeatureDialog variable    
    NUMBER nvPostgresSelected;   
    NUMBER nReturn;  
    NUMBER nResult1,nResult2,nResult3,nResult4;
    STRING szCaption1,szCaption2;  
    STRING szField1,szField2;
    NUMBER nvVar2;
begin  
//可能需要改写，检查web_server和web_client的exe，如果有的话  
//OnCheckProgramRun();
	
Dlg_Start:
	 if(SYSINFO.WINNT.bAdmin_Logged_On == FALSE) then
        WriteLogFile( MSG_ERRO_CHECK_ADMIN );
    	MessageBox(MSG_ERRO_CHECK_ADMIN,WARNING);
        abort;
     endif;

Dlg_SdFeatureTree:        
    szTitle    = "";
    szMsg      = ""; 
    Disable(BACKBUTTON);
	
	
	if( !REMOVEONLY ) then
        
        MessageBox (MSG_INFO_REPEAT_INSTALL, INFORMATION);
 	    abort;       	 
    else		
        //Disable( DIALOGCACHE );
        szMsg  = "Are you sure you want to remove Redis for NetBrain?";
    Enable(BACKBUTTON);
	 if ( REMOVEALL ) then
		nResult = MessageBox(szMsg, MB_YESNO );
		if (nResult != IDYES ) then
            
            if( REMOVEONLY ) then
                // In REMOVEONLY mode, abort the setup.
                abort;
            else
                // In non-REMOVEONLY mode, redisplay the previous dialog.
			    goto Dlg_Start;
            endif;
        else
        FeatureSelectItem (MEDIA, FeatureRedis, FALSE); 
		endif;
	endif;
    endif;    
         
	Disable(CANCELBUTTON);
    nResult = NEXT;  
	SdShowMsg ("Removing, please wait...", TRUE); 
	SdShowMsg ("", FALSE);   
	//DeleteRegistryWeb();
	NumToStr(svFeatureRedis,nResult2);
	   		
    Enable(STATUSEX);     
    if (nResult = BACK) goto Dlg_Start;         
end;

//---------------------------------------------------------------------------
// OnEnd
//
// The OnEnd event is called at the end of the setup. This event is not
// called if the setup is aborted.
//---------------------------------------------------------------------------
function OnEnd()
    STRING szTitle, szMsg1, szMsg2, szOpt1, szOpt2;
    NUMBER bOpt1, bOpt2;
    NUMBER nvType,nvSize;
begin
	if (hMutex) then
		CloseHandle(hMutex);
	endif;	
	if (!REMOVEONLY) then
	if(SYSINFO.bIsWow64) then 
    REGDB_OPTIONS = REGDB_OPTIONS | REGDB_OPTION_WOW64_64KEY;
	nvType = REGDB_STRING; 
    nvSize = -1;  
    if (RegDBSetDefaultRoot (HKEY_LOCAL_MACHINE) = 0) then
        RegDBSetKeyValueEx(REGROOT,REGKEY_REDIS,nvType,svFeatureRedis,nvSize);
    endif;
    REGDB_OPTIONS = REGDB_OPTIONS & ~REGDB_OPTION_WOW64_64KEY;
    endif;
    WriteRegistryDump();
    endif;    
    
    if (REMOVEONLY) then
    DeleteRegistryDump();
    DeleteFolder(DUMP_PATH);
    endif;
end;
